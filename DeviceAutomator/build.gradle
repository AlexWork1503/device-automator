apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

version = '0.2.0-SNAPSHOT'

android {
    compileSdkVersion 26
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 3
        versionName version

        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
    }

    lintOptions {
        warning 'InvalidPackage'
        textReport true
        textOutput 'stdout'
    }
}

dependencies {
    compile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
    compile 'com.android.support.test:runner:0.5'
    compile 'com.android.support.test:rules:0.5'
    compile 'com.android.support:support-compat:25.0.0'

    androidTestCompile 'com.google.dexmaker:dexmaker-mockito:1.2'

    testCompile 'org.robolectric:robolectric:3.3.1'
    testCompile 'org.mockito:mockito-core:1.10.19'
}

/* maven deploy + signing */
task javadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError false
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
    classifier = 'javadoc'
    from javadocs.destinationDir
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives javadocsJar
    archives sourcesJar
}

signing {
    required { !version.endsWith("SNAPSHOT") }
    sign configurations.archives
}

def sonatypeUsername = System.env['SONATYPE_USERNAME']
def sonatypePassword = System.env['SONATYPE_PASSWORD']

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.setArtifactId 'device-automator'

            pom.project {
                name 'device-automator'
                groupId 'com.lukekorth'
                version android.defaultConfig.versionName
                packaging 'jar'
                description 'An easy to use, Espresso like, syntax on top of the Android UI Automator testing framework'
                url 'https://github.com/lkorth/device-automator'

                scm {
                    url 'scm:git@github.com:lkorth/device-automator.git'
                    connection 'scm:git@github.com:lkorth/device-automator.git'
                    developerConnection 'scm:git@github.com:lkorth/device-automator.git'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'lkorth'
                        name 'Luke Korth'
                    }
                }
            }
        }
    }
}
